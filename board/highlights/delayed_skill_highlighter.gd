extends Node2D


class HighlightedSkill extends RefCounted:
	var unit: Unit
	var skill: Skill
	var skill_highlight: Node2D = null


@export var skill_highlight_packed_scene: PackedScene

# Only enemies use delayed skills so it's fine to just use one color
@export var cell_highlight_color: Color


# Array<HighlightedSkill>
var _highlighted_skills: Array = []


func highlight(unit: Unit, skill: Skill, target_cells: Array) -> void:
	var skill_highlight: Node2D = skill_highlight_packed_scene.instantiate()
	
	add_child(skill_highlight)
	skill_highlight.show_highlight(target_cells, cell_highlight_color)
	
	var highlighted_skill: HighlightedSkill = HighlightedSkill.new()
	highlighted_skill.unit = unit
	highlighted_skill.skill = skill
	highlighted_skill.skill_highlight = skill_highlight
	
	_highlighted_skills.push_back(highlighted_skill)


# Removes the highlight of the delayed skill associated to the given unit
func remove(unit: Unit, skill: Skill) -> void:
	var highlighted_skill: HighlightedSkill = _find_highlighted_skill(unit, skill)
	
	if highlighted_skill != null:
		_remove_highlighted_skill(highlighted_skill)


# Removes all highlights of a delayed skill associated to the given unit.
# A unit can have several highlights active at the same time if they
# use a delayed skill before the previous one triggers.
func remove_all(unit: Unit) -> void:
	var highlights_to_remove: Array = []
	
	for highlighted_skill in _highlighted_skills:
		if highlighted_skill.unit == unit:
			highlights_to_remove.push_back(highlighted_skill)
	
	for highlighted_skill in highlights_to_remove:
		_remove_highlighted_skill(highlighted_skill)


# Stops the animation (which frees the highlight at the end)
# and removes the highlighted skill from the list
func _remove_highlighted_skill(highlighted_skill: HighlightedSkill) -> void:
	highlighted_skill.skill_highlight.stop()
	
	_highlighted_skills.erase(highlighted_skill)


func _find_highlighted_skill(unit: Unit, skill: Skill) -> HighlightedSkill:
	for highlighted_skill in _highlighted_skills:
		if highlighted_skill.unit == unit and highlighted_skill.skill == skill:
			return highlighted_skill
	
	return null
